function extract --description "extract any archive"
    if test -z "$argv[1]"
        echo "usage: extract <file>"
        return 1
    end
    if not test -f "$argv[1]"
        echo "extract: '$argv[1]' not a file"
        return 1
    end
    switch "$argv[1]"
        case '*.tar.bz2'
            tar xjf "$argv[1]"
        case '*.tar.gz'
            tar xzf "$argv[1]"
        case '*.tar.xz'
            tar xJf "$argv[1]"
        case '*.tar.zst'
            tar --zstd -xf "$argv[1]"
        case '*.tar'
            tar xf "$argv[1]"
        case '*.bz2'
            bunzip2 "$argv[1]"
        case '*.gz'
            gunzip "$argv[1]"
        case '*.zip'
            unzip "$argv[1]"
        case '*.7z'
            7z x "$argv[1]"
        case '*.rar'
            unrar x "$argv[1]"
        case '*.xz'
            xz -d "$argv[1]"
        case '*'
            echo "extract: unknown format '$argv[1]'"
            return 1
    end
end
